!=================================================================================================================================
! Copyright (c) 2010-2016  Prof. Claus-Dieter Munz 
! This file is part of FLEXI, a high-order accurate framework for numerically solving PDEs with discontinuous Galerkin methods.
! For more information see https://www.flexi-project.org and https://nrg.iag.uni-stuttgart.de/
!
! FLEXI is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License 
! as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
!
! FLEXI is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty
! of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License v3.0 for more details.
!
! You should have received a copy of the GNU General Public License along with FLEXI. If not, see <http://www.gnu.org/licenses/>.
!=================================================================================================================================

!==================================================================================================================================
!> Interpolates the interior volume data (stored at the Gauss or Gauss-Lobatto points) to the surface
!> integration points, using fast 1D Interpolation and store in global side structure
!==================================================================================================================================
SUBROUTINE ProlongToFace(&
#ifdef WITHnVar
    TP_nVar,&
#endif
    Nloc,Uvol,Uface_master,Uface_slave,L_Minus,L_Plus,doMPISides)
! MODULES
USE MOD_Mesh_Vars,          ONLY: nElems
USE MOD_Mesh_Vars,          ONLY: SideToElem
USE MOD_Mesh_Vars,          ONLY: firstMPISide_YOUR, lastMPISide_MINE, nSides
USE MOD_Mesh_Vars,          ONLY: S2V2
#if FV_ENABLED
USE MOD_FV_Vars,            ONLY: FV_Elems,FV_Elems_master,FV_Elems_slave
#endif
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------------------
! INPUT/OUTPUT VARIABLES
#ifdef WITHnVar
INTEGER,INTENT(IN)              :: TP_nVar
#endif
INTEGER,INTENT(IN)              :: Nloc
LOGICAL,INTENT(IN)              :: doMPISides  != .TRUE. only YOUR MPISides are filled, =.FALSE. BCSides +InnerSides +MPISides MINE
REAL,INTENT(IN)                 :: L_Minus(0:Nloc),L_Plus(0:Nloc)
REAL,INTENT(IN)                 :: Uvol(TP_nVar,0:Nloc,0:Nloc,0:PP_NlocZ,1:nElems)
REAL,INTENT(INOUT)              :: Uface_master(TP_nVar,0:Nloc,0:PP_NlocZ,1:nSides)
REAL,INTENT(INOUT)              :: Uface_slave( TP_nVar,0:Nloc,0:PP_NlocZ,1:nSides)
!----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
INTEGER                         :: p,q,firstSideID,lastSideID
INTEGER                         :: ElemID,nbElemID,locSide,nblocSide,SideID,flip
REAL                            :: Uface(TP_nVar,0:Nloc,0:PP_NlocZ)
!==================================================================================================================================
IF(doMPISides)THEN
  firstSideID = firstMPISide_YOUR
   lastSideID = nSides
ELSE
  firstSideID = 1
   lastSideID =  lastMPISide_MINE
END IF

DO SideID=firstSideID,lastSideID
  ElemID    = SideToElem(S2E_ELEM_ID,SideID)
  nbElemID  = SideToElem(S2E_NB_ELEM_ID,SideID)

  !master sides
  IF(ElemID.GT.0)THEN
    locSide = SideToElem(S2E_LOC_SIDE_ID,SideID)
    flip    = 0

    IF(PP_NodeType.EQ.1.AND.FV_Elems(ElemID).EQ.0)THEN
      CALL EvalElemFaceG(&
#if WITHnVar
          TP_nVar,&
#endif    
          Nloc,UVol(:,:,:,:,ElemID),Uface,L_Minus,L_Plus,locSide)
    ELSE
      CALL EvalElemFaceGL(&
#if WITHnVar
          TP_nVar,&
#endif    
          Nloc,UVol(:,:,:,:,ElemID),Uface,locSide)
    END IF

    DO q=0,PP_NlocZ; DO p=0,Nloc
      Uface_master(:,p,q,SideID)=Uface(:,S2V2(1,p,q,flip,locSide),S2V2(2,p,q,flip,locSide))
    END DO; END DO
#if FV_ENABLED
    FV_Elems_master(SideID) = FV_Elems(ElemID)
#endif
  END IF

  !slave side (ElemID,locSide and flip =-1 if not existing)
  IF(nbElemID.GT.0)THEN
    nblocSide = SideToElem(S2E_NB_LOC_SIDE_ID,SideID)
    flip        = SideToElem(S2E_FLIP,SideID)

    IF(PP_NodeType.EQ.1.AND.FV_Elems(nbElemID).EQ.0)THEN
      CALL EvalElemFaceG(&
#if WITHnVar
          TP_nVar,&
#endif    
          Nloc,UVol(:,:,:,:,nbElemID),Uface,L_Minus,L_Plus,nblocSide)
    ELSE
      CALL EvalElemFaceGL(&
#if WITHnVar
          TP_nVar,&
#endif    
          Nloc,UVol(:,:,:,:,nbElemID),Uface,nblocSide)
    END IF

    DO q=0,PP_NlocZ; DO p=0,Nloc
      Uface_slave( :,p,q,SideID)=Uface(:,S2V2(1,p,q,flip,nblocSide),S2V2(2,p,q,flip,nblocSide))
    END DO; END DO
#if FV_ENABLED
    FV_Elems_slave(SideID) = FV_Elems(nbElemID)
#endif
  END IF
END DO

END SUBROUTINE ProlongToFace


!==================================================================================================================================
!> Interpolates the element volume data stored at Gauss points
!==================================================================================================================================
PURE SUBROUTINE EvalElemFaceG(&
#ifdef WITHnVar
    TP_nVar,&
#endif
    Nloc,Uvol,Uface,L_Minus,L_Plus,locSide)
! MODULES
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------------------
! INPUT/OUTPUT VARIABLES
#ifdef WITHnVar
INTEGER,INTENT(IN)              :: TP_nVar
#endif
INTEGER,INTENT(IN)              :: Nloc
INTEGER,INTENT(IN)              :: locSide
REAL,INTENT(IN)                 :: L_Minus(0:Nloc),L_Plus(0:Nloc)
REAL,INTENT(IN)                 :: Uvol( TP_nVar,0:Nloc,0:Nloc,0:PP_NlocZ)
REAL,INTENT(OUT)                :: Uface(TP_nVar,0:Nloc,0:PP_NlocZ)
!----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
INTEGER                         :: l
!==================================================================================================================================
SELECT CASE(locSide)
CASE(XI_MINUS)
  Uface=Uvol(:,0,:,:)*L_Minus(0)
  DO l=1,Nloc
    Uface=Uface+Uvol(:,l,:,:)*L_Minus(l)
  END DO ! l
CASE(ETA_MINUS)
  Uface=Uvol(:,:,0,:)*L_Minus(0)
  DO l=1,Nloc
    Uface=Uface+Uvol(:,:,l,:)*L_Minus(l)
  END DO ! l
CASE(ZETA_MINUS)
  Uface=Uvol(:,:,:,0)*L_Minus(0)
  DO l=1,Nloc
    Uface=Uface+Uvol(:,:,:,l)*L_Minus(l)
  END DO ! l
CASE(XI_PLUS)
  Uface=Uvol(:,0,:,:)*L_Plus(0)
  DO l=1,Nloc
    Uface=Uface+Uvol(:,l,:,:)*L_Plus(l)
  END DO ! l
CASE(ETA_PLUS)
  Uface=Uvol(:,:,0,:)*L_Plus(0)
  DO l=1,Nloc
    Uface=Uface+Uvol(:,:,l,:)*L_Plus(l)
  END DO ! l
CASE(ZETA_PLUS)
  Uface=Uvol(:,:,:,0)*L_Plus(0)
  DO l=1,Nloc
    Uface=Uface+Uvol(:,:,:,l)*L_Plus(l)
  END DO ! l
END SELECT
END SUBROUTINE EvalElemFaceG


!==================================================================================================================================
!> Interpolates the element volume data stored at Gauss-Lobatto points
!==================================================================================================================================
PURE SUBROUTINE EvalElemFaceGL(&
#ifdef WITHnVar
    TP_nVar,&
#endif
    Nloc,Uvol,Uface,locSide)
! MODULES
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------------------
! INPUT/OUTPUT VARIABLES
#ifdef WITHnVar
INTEGER,INTENT(IN)              :: TP_nVar
#endif
INTEGER,INTENT(IN)              :: Nloc
INTEGER,INTENT(IN)              :: locSide
REAL,INTENT(IN)                 :: Uvol( TP_nVar,0:Nloc,0:Nloc,0:PP_NlocZ)
REAL,INTENT(OUT)                :: Uface(TP_nVar,0:Nloc,0:PP_NlocZ)
!----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
!==================================================================================================================================
SELECT CASE(locSide)
CASE(XI_MINUS)
  Uface=Uvol(:,0,:,:)
CASE(ETA_MINUS)
  Uface=Uvol(:,:,0,:)
CASE(ZETA_MINUS)
  Uface=Uvol(:,:,:,0)
CASE(XI_PLUS)
  Uface=Uvol(:,Nloc,:,:)
CASE(ETA_PLUS)
  Uface=Uvol(:,:,Nloc,:)
CASE(ZETA_PLUS)
  Uface=Uvol(:,:,:,Nloc)
END SELECT
END SUBROUTINE EvalElemFaceGL

