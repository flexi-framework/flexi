!=================================================================================================================================
! Copyright (c) 2010-2016  Prof. Claus-Dieter Munz 
! This file is part of FLEXI, a high-order accurate framework for numerically solving PDEs with discontinuous Galerkin methods.
! For more information see https://www.flexi-project.org and https://nrg.iag.uni-stuttgart.de/
!
! FLEXI is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License 
! as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
!
! FLEXI is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty
! of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License v3.0 for more details.
!
! You should have received a copy of the GNU General Public License along with FLEXI. If not, see <http://www.gnu.org/licenses/>.
!=================================================================================================================================
#include "flexi.h"

!==================================================================================================================================
!> In this routine, the surface integral will be computed 
!> Polynomial degree is either N or NOver (overintegration)
!==================================================================================================================================
SUBROUTINE SurfInt(&
#ifdef WITHnVars
        TP_nVar, &
#endif        
        Nloc,Flux_master,Flux_slave,Ut,doMPISides,L_HatMinus,L_HatPlus)
! MODULES
USE MOD_Globals
USE MOD_PreProc
USE MOD_Mesh_Vars ,ONLY: SideToElem,nSides
USE MOD_Mesh_Vars ,ONLY: firstMPISide_YOUR,lastMPISide_MINE
USE MOD_Mesh_Vars ,ONLY: S2V2,nElems
#if FV_ENABLED
USE MOD_FV_Vars   ,ONLY: FV_Elems_master,FV_Elems_slave,FV_w_inv
#endif /* FV_ENABLED */
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------------------
! INPUT/OUTPUT VARIABLES
#ifdef WITHnVars
INTEGER,INTENT(IN) :: TP_nVar     !< number of variables
#endif        
INTEGER,INTENT(IN) :: Nloc        !< (IN) Polynomial degree either N or NOver in case of overintegration
LOGICAL,INTENT(IN) :: doMPISides  !<= .TRUE. only MPISides_YOUR+MPIMortar are filled
                                  !<=.FALSE. BCSides+(Mortar-)InnerSides+MPISides_MINE
REAL,INTENT(IN)    :: Flux_master(1:TP_nVar,0:Nloc,0:PP_NlocZ,nSides) !< (IN) Flux on master side
REAL,INTENT(IN)    :: Flux_slave (1:TP_nVar,0:Nloc,0:PP_NlocZ,nSides) !< (IN) Flux on slave side
!> (IN) Lagrange polynomials evaluated at \f$\xi=+1\f$ and \f$\xi=-1\f$ and premultiplied by mass matrix
REAL,INTENT(IN)    :: L_HatPlus(0:Nloc),L_HatMinus(0:Nloc)
REAL,INTENT(INOUT) :: Ut(TP_nVar,0:Nloc,0:Nloc,0:PP_NlocZ,1:nElems)   !< (INOUT) Time derivative of the solution
!----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
INTEGER            :: ElemID,nbElemID,locSideID,nblocSideID,SideID,p,q,flip
INTEGER            :: firstSideID,lastSideID
REAL               :: FluxTmp(1:TP_nVar,0:Nloc,0:PP_NlocZ)
!==================================================================================================================================

IF(doMPISides)THEN
  ! MPI YOUR
  firstSideID = firstMPISide_YOUR
   lastSideID = nSides
ELSE
  ! inner sides and MPI mine
  firstSideID = 1
   lastSideID = lastMPISide_MINE
END IF

DO SideID=firstSideID,lastSideID
  ElemID      = SideToElem(S2E_ELEM_ID,   SideID)
  nbElemID    = SideToElem(S2E_NB_ELEM_ID,SideID)

  ! master sides
  IF(ElemID.GT.0)THEN
    locSideID   = SideToElem(S2E_LOC_SIDE_ID,SideID)
    flip        = 0
    ! orient flux to fit flip and locSide to element local system
    DO q=0,PP_NlocZ; DO p=0,Nloc
      FluxTmp(:,p,q)=Flux_master(:,S2V2(1,p,q,flip,locSideID),S2V2(2,p,q,flip,locSideID),SideID)
    END DO; END DO ! p,q
#if FV_ENABLED
    IF (FV_Elems_master(SideID).EQ.0) THEN ! DG Element
#endif /* FV_ENABLED */
#if   (PP_NodeType==1)
      CALL DoSurfInt(&
#ifdef WITHnVars
          TP_nVar, &
#endif          
          Nloc,FluxTmp,L_HatMinus,   L_HatPlus,      locSideID,Ut(:,:,:,:,ElemID))
#elif (PP_NodeType==2)
      CALL DoSurfInt(&
#ifdef WITHnVars
          TP_nVar, &
#endif          
          Nloc,FluxTmp,L_HatMinus(0),L_HatPlus(Nloc),locSideID,Ut(:,:,:,:,ElemID))
#endif
#if FV_ENABLED
    ELSE
      CALL SurfInt_FV(&
#ifdef WITHnVars
          TP_nVar, &
#endif          
      Nloc,FluxTmp,FV_w_inv,locSideID,Ut(:,:,:,:,ElemID))
    END IF
#endif /* FV_ENABLED */
  END IF

  ! slave sides
  IF(nbElemID.GT.0)THEN
    nblocSideID = SideToElem(S2E_NB_LOC_SIDE_ID,SideID)
    flip        = SideToElem(S2E_FLIP,SideID)
    ! orient flux to fit flip and locSide to element local system
    DO q=0,PP_NlocZ; DO p=0,Nloc
      ! p,q are in the master RHS system, they need to be transformed to the slave volume system using S2V2 mapping
      FluxTmp(:,S2V2(1,p,q,flip,nblocSideID),S2V2(2,p,q,flip,nblocSideID))=-Flux_slave(:,p,q,SideID)
    END DO; END DO ! p,q
#if FV_ENABLED
    IF (FV_Elems_slave(SideID).EQ.0) THEN ! DG Element
#endif /* FV_ENABLED */
#if   (PP_NodeType==1)
      CALL DoSurfInt(&
#ifdef WITHnVars
          TP_nVar, &
#endif          
          Nloc,FluxTmp,L_HatMinus,   L_HatPlus,      nblocSideID,Ut(:,:,:,:,nbElemID))
#elif (PP_NodeType==2)
      CALL DoSurfInt(&
#ifdef WITHnVars
          TP_nVar, &
#endif          
          Nloc,FluxTmp,L_HatMinus(0),L_HatPlus(Nloc),nblocSideID,Ut(:,:,:,:,nbElemID))
#endif
#if FV_ENABLED
    ELSE
      CALL SurfInt_FV(&
#ifdef WITHnVars
          TP_nVar, &
#endif          
          Nloc,FluxTmp,FV_w_inv,nblocSideID,Ut(:,:,:,:,nbElemID))
    END IF
#endif /* FV_ENABLED */
  END IF
END DO ! SideID=1,nSides
END SUBROUTINE SurfInt

#if FV_ENABLED
!==================================================================================================================================
!> Update DG time derivative with corresponding SurfInt contribution
!> Weak surface integral
!==================================================================================================================================
PURE SUBROUTINE SurfInt_FV(&
#ifdef WITHnVars
        TP_nVar, &
#endif        
        Nloc,Flux,FV_w_inv,locSideID,Ut)
!MODULES
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------------------
! INPUT/OUTPUT VARIABLES
#ifdef WITHnVars
INTEGER,INTENT(IN) :: TP_nVar
#endif        
INTEGER,INTENT(IN) :: Nloc
REAL,INTENT(IN)    :: Flux(TP_nVar,0:Nloc,0:PP_NlocZ)
REAL,INTENT(IN)    :: FV_w_inv
INTEGER,INTENT(IN) :: locSideID
REAL,INTENT(INOUT) :: Ut(TP_nVar,0:Nloc,0:Nloc,0:PP_NlocZ)
!----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
!==================================================================================================================================
SELECT CASE(locSideID)
CASE(XI_MINUS)
  Ut(:,0,:,:)   =Ut(:,0,:,:)   +Flux*FV_w_inv
CASE(ETA_MINUS)
  Ut(:,:,0,:)   =Ut(:,:,0,:)   +Flux*FV_w_inv
CASE(ZETA_MINUS)
  Ut(:,:,:,0)   =Ut(:,:,:,0)   +Flux*FV_w_inv
CASE(XI_PLUS)
  Ut(:,Nloc,:,:)=Ut(:,Nloc,:,:)+Flux*FV_w_inv
CASE(ETA_PLUS)
  Ut(:,:,Nloc,:)=Ut(:,:,Nloc,:)+Flux*FV_w_inv
CASE(ZETA_PLUS)
  Ut(:,:,:,Nloc)=Ut(:,:,:,Nloc)+Flux*FV_w_inv
END SELECT !locSideID
END SUBROUTINE SurfInt_FV
#endif

!==================================================================================================================================
!> \brief Update DG time derivative with corresponding SurfInt contribution
!>
!> Takes the time derivative of a single element and the surface flux on a single side as input and performs the surface integral.
!> The time derivative will be updated. The Flux has to be provided in the volume coordinate system!
!==================================================================================================================================
SUBROUTINE DoSurfInt(&
#ifdef WITHnVars
        TP_nVar, &
#endif        
        Nloc,Flux,L_HatMinus,L_HatPlus,locSideID,Ut)
!MODULES
IMPLICIT NONE
!----------------------------------------------------------------------------------------------------------------------------------
! INPUT/OUTPUT VARIABLES
#ifdef WITHnVars
INTEGER,INTENT(IN) :: TP_nVar                          !< Number of variables
#endif
INTEGER,INTENT(IN) :: Nloc                             !< Polynomial degree either N or NOver in case of overintegration
INTEGER,INTENT(IN) :: locSideID                        !< sideID of side in global system
REAL,INTENT(IN)    :: Flux(TP_nVar,0:Nloc,0:PP_NlocZ)  !< Flux on side, in volume system of respective element
!> Lagrange polynomials evaluated at \f$i \xi=+1 \f$ and \f$ \xi=-1 \f$ premultiplied by mass matrix
#if (PP_NodeType==1)
REAL,INTENT(IN)    :: L_HatPlus(0:Nloc),L_HatMinus(0:Nloc)
#elif (PP_NodeType==2)
REAL,INTENT(IN)    :: L_HatPlus,L_HatMinus
#endif
REAL,INTENT(INOUT) :: Ut(TP_nVar,0:Nloc,0:Nloc,0:PP_NlocZ) !< Time derivative (will be updated)
!----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
#if (PP_NodeType==1)
INTEGER            :: l
#endif
!==================================================================================================================================
SELECT CASE(locSideID)
#if (PP_NodeType==1)
CASE(XI_MINUS)
  DO l=0,Nloc
    Ut(:,l,:,:) =Ut(:,l,:,:)   +Flux*L_hatMinus(l)
  END DO
CASE(ETA_MINUS)
  DO l=0,Nloc
    Ut(:,:,l,:) =Ut(:,:,l,:)   +Flux*L_hatMinus(l)
  END DO
CASE(ZETA_MINUS)
  DO l=0,Nloc
    Ut(:,:,:,l) =Ut(:,:,:,l)   +Flux*L_hatMinus(l)
  END DO
CASE(XI_PLUS)
  DO l=0,Nloc
    Ut(:,l,:,:) =Ut(:,l,:,:)   +Flux*L_hatPlus(l)
  END DO
CASE(ETA_PLUS)
  DO l=0,Nloc
    Ut(:,:,l,:) =Ut(:,:,l,:)   +Flux*L_hatPlus(l)
  END DO
CASE(ZETA_PLUS)
  DO l=0,Nloc
    Ut(:,:,:,l) =Ut(:,:,:,l)   +Flux*L_hatPlus(l)
  END DO
#elif (PP_NodeType==2)
CASE(XI_MINUS)
  Ut(:,0,:,:)   =Ut(:,0,:,:)   +Flux*L_hatMinus
CASE(ETA_MINUS)
  Ut(:,:,0,:)   =Ut(:,:,0,:)   +Flux*L_hatMinus
CASE(ZETA_MINUS)
  Ut(:,:,:,0)   =Ut(:,:,:,0)   +Flux*L_hatMinus
CASE(XI_PLUS)
  Ut(:,Nloc,:,:)=Ut(:,Nloc,:,:)+Flux*L_hatPlus
CASE(ETA_PLUS)
  Ut(:,:,Nloc,:)=Ut(:,:,Nloc,:)+Flux*L_hatPlus
CASE(ZETA_PLUS)
  Ut(:,:,:,Nloc)=Ut(:,:,:,Nloc)+Flux*L_hatPlus
#endif
END SELECT !locSideID
END SUBROUTINE DoSurfInt

